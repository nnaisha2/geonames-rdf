PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

CONSTRUCT {
  ?uri ?p ?o .
  ?doc ?dp ?dv .
}
WHERE {
  {
    ?uri a gn:Feature ;
         ?p ?o .

    # Filter out gn:parentCountry only for country entities
    FILTER(!(?p = gn:parentCountry && EXISTS {
      ?uri a gn:Feature ;
           gn:featureClass gn:A ;
           gn:featureCode gn:A.PCLI .
    }))

    # Filter out gn:nearbyFeatures triples for ADM1 features
    FILTER( !(
      ?p = gn:nearbyFeatures &&
      EXISTS {
        ?uri gn:featureClass gn:A ;
             gn:featureCode gn:A.ADM1 .
      }
    ))
  }
  UNION
  {
    ?doc a foaf:Document ;
         ?dp ?dv .
  }
}
ORDER BY ?uri