PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wgs84_pos: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

CONSTRUCT {
  # --- Feature triples ---
  ?uri a gn:Feature ;
    gn:name ?name ;
    rdfs:isDefinedBy ?definitionDoc ;
    gn:asciiname ?asciiname ;
    gn:countryCode ?countryCode ;
    gn:featureClass ?featureClass ;
    gn:featureCode ?featureCode ;
    gn:population ?population ;
    wgs84_pos:lat ?latitudeDecimal ;
    wgs84_pos:long ?longitudeDecimal ;
    gn:nearbyFeatures ?nearbyEndpoint ;
    gn:locationMap ?locationMapUri ;
    gn:parentCountry ?parentCountry ;
    gn:parentADM1 ?parentAdm1 ;
    gn:parentADM2 ?parentAdm2 ;
    gn:parentADM3 ?parentAdm3 ;
    gn:parentADM4 ?parentAdm4 ;
    gn:elevation ?elevation ;
    gn:dem ?dem ;
    gn:timezone ?timezone .

  # --- Document triples ---
  ?definitionDoc a foaf:Document ;
    foaf:primaryTopic ?uri ;
    cc:license <https://creativecommons.org/licenses/by/4.0/> ;
    cc:attributionURL <https://www.geonames.org> ;
    cc:attributionName "GeoNames"^^xsd:string ;
    dcterms:modified ?modDate .
    # dcterms:created ?createdDate .
}
WHERE {
  SERVICE <x-sparql-anything:> {
    fx:properties
      fx:location ?_SOURCE ;
      fx:ondisk "/tmp/sparql-anything-geonames" ;
      fx:ondisk.reuse false ;
      fx:null-string "" ;
      fx:csv.delimiter "\t" ;
      fx:csv.headers true ;
    .

    # Minimal required fields
    ?s xyz:geonameid ?id ;
       xyz:name ?name ;
       xyz:asciiname ?asciiname ;
       xyz:country%20code ?countryCode ;
       xyz:feature%20class ?featureClassString ;
       xyz:latitude ?latitude ;
       xyz:longitude ?longitude ;
       xyz:population ?population .

    # Optional fields
    OPTIONAL { ?s xyz:feature%20code ?featureCodeString . }
    OPTIONAL { ?s xyz:elevation ?elevation . }
    OPTIONAL { ?s xyz:dem ?dem . }
    OPTIONAL { ?s xyz:timezone ?timezone . }
    OPTIONAL { ?s xyz:modification%20date ?modDateRaw . }
    OPTIONAL { ?s xyz:adm1 ?adm1FullCode . }
    OPTIONAL { ?s xyz:adm2 ?adm2FullCode . }
    OPTIONAL { ?s xyz:admin3%20code ?admin3LocalCode . }
    OPTIONAL { ?s xyz:admin4%20code ?admin4LocalCode . }

    # BINDs for URIs and transformed values
    BIND(URI(CONCAT("https://sws.geonames.org/", ?id, "/")) AS ?uri)
    BIND(URI(CONCAT("https://sws.geonames.org/", ?id, "/about.rdf")) AS ?definitionDoc)
    BIND(URI(CONCAT("https://www.geonames.org/ontology#", ?featureClassString)) AS ?featureClass)
    BIND(
      IF(BOUND(?featureCodeString) && ?featureCodeString != "",
         URI(CONCAT("https://www.geonames.org/ontology#", ?featureClassString, ".", ?featureCodeString)),
         ""
      ) AS ?featureCode
    )
    BIND(xsd:decimal(?latitude) AS ?latitudeDecimal)
    BIND(xsd:decimal(?longitude) AS ?longitudeDecimal)
    BIND(URI(CONCAT("https://sws.geonames.org/", ?id, "/nearby.rdf")) AS ?nearbyEndpoint)
    BIND(URI(CONCAT("https://www.geonames.org/", ?id, "/", ENCODE_FOR_URI(REPLACE(LCASE(?asciiname), " ", "-")), ".html")) AS ?locationMapUri)
    OPTIONAL { ?s xyz:modification%20date ?modDateRaw . }
    BIND(xsd:date(?modDateRaw) AS ?modDate)

    # Admin code string construction for ADM3/ADM4
    BIND(
      IF(BOUND(?adm2FullCode) && BOUND(?admin3LocalCode) && ?admin3LocalCode != "" && ?admin3LocalCode != "NONE",
         CONCAT(?adm2FullCode, ".", ?admin3LocalCode),
         ""
      ) AS ?adm3FullCode
    )
    BIND(
      IF(?adm3FullCode != "" && BOUND(?admin4LocalCode) && ?admin4LocalCode != "" && ?admin4LocalCode != "NONE",
         CONCAT(?adm3FullCode, ".", ?admin4LocalCode),
         ""
      ) AS ?adm4FullCode
    )
  }

  # --- Loading country codes---
  {
    SELECT ?countryCode ?countryUri
    WHERE {
      SERVICE <x-sparql-anything:> {
        fx:properties fx:location "data/country-codes.csv" ;
                      fx:csv.headers "true" ;
                      fx:csv.delimiter "\t" .
        ?row xyz:countryId ?countryId ;
             xyz:countryCode ?countryCode ;
             xyz:name ?countryName .
        BIND(URI(CONCAT("https://sws.geonames.org/", ?countryId, "/")) AS ?countryUri)
      }
    }
  }

  # --- Join by country code ---
  FILTER(?countryCode != "")
  BIND(?countryUri AS ?parentCountry)

  OPTIONAL {
    FILTER(BOUND(?adm1FullCode) && ?adm1FullCode != "")
    ?parentAdm1 gn:admin1Code ?adm1FullCode .
    FILTER(?parentAdm1 != ?uri)
  }
  OPTIONAL {
    FILTER(BOUND(?adm2FullCode) && ?adm2FullCode != "")
    ?parentAdm2 gn:admin2Code ?adm2FullCode .
    FILTER(?parentAdm2 != ?uri)
  }
  OPTIONAL {
    FILTER(?adm3FullCode != "")
    ?parentAdm3 gn:admin3Code ?adm3FullCode .
    FILTER(?parentAdm3 != ?uri)
  }
  OPTIONAL {
    FILTER(?adm4FullCode != "")
    ?parentAdm4 gn:admin4Code ?adm4FullCode .
    FILTER(?parentAdm4 != ?uri)
  }
}
