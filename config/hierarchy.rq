PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>

CONSTRUCT {
  ?parent gn:childrenFeatures ?child .
  ?child gn:parentFeature ?parent .  
}
WHERE {
  SERVICE <x-sparql-anything:> {
    fx:properties
      fx:location "data/hierarchy.csv" ;
      fx:csv.delimiter "\t" ;
      fx:csv.headers true .

    ?s xyz:parentId ?parentId ;
       xyz:childId ?childId .

    BIND(URI(CONCAT("https://sws.geonames.org/", STR(?parentId), "/")) AS ?parent)
    BIND(URI(CONCAT("https://sws.geonames.org/", STR(?childId), "/")) AS ?child)
  }
}