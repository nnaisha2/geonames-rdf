PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?featureURI
    gn:postalCode ?postalCode ;
    gn:hasPostalCode ?postalCodeURI ;
    gn:admin1Name ?admin1Name ;
    gn:admin1Code ?admin1Code ;
    gn:admin2Name ?admin2Name ;
    gn:admin2Code ?admin2Code ;
    gn:locationAccuracy ?accuracy ;
    gn:postalPlaceName ?placeName ;
    gn:postalPlaceName ?municipalityName .
}
WHERE {
  # Load postal codes CSV
  SERVICE <x-sparql-anything:> {
    fx:properties
      fx:location "data/postal-codes-DE.csv" ;
      fx:csv.headers "true" ;
      fx:csv.delimiter "\t" ;
      fx:csv.null-string "" .

    ?row
      xyz:postal%20code ?postalCode ;
      xyz:country%20code ?countryCode ;
      xyz:place%20name ?placeName ;
      xyz:admin%20name1 ?admin1Name ;
      xyz:admin%20code1 ?admin1Code ;
      xyz:admin%20name3 ?municipalityName ;
      xyz:admin%20code3 ?admin3Code .
      OPTIONAL { ?row xyz:admin%20name2 ?admin2Name . }
      OPTIONAL { ?row xyz:admin%20code2 ?admin2Code . }
      OPTIONAL { ?row xyz:accuracy ?accuracy . }
  }

  # Join with admin-codes.ttl 
  ?featureURI gn:admin3Code ?admin3CodeTTL .
  FILTER(STRENDS(?admin3CodeTTL, CONCAT(".", ?admin3Code)))

  # Postal code URI
  BIND(IRI(CONCAT("https://sws.geonames.org/postal/", ?countryCode, "/", ?postalCode)) AS ?postalCodeURI)
}
